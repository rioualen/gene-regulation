# Set parameters
if not "qsub" in config.keys():
    sys.exit("The parameter qsub must be defined in the config file")

if not "version" in config["genome"].keys():
    sys.exit("Genome version must be defined in the config file")

if not "fasta_file" in config["genome"].keys():
    sys.exit("Fasta file must be defined in the config file")

if not "genome" in config["dir"].keys():
    sys.exit("Genome dir must be defined in the dir section of the config file")

if not "seq_type" in config["metadata"].keys():
    sys.exit("Sequencing type (single or paired ends) must be defined in the config file")

if config["metadata"]["seq_type"] == "pe" and not "strands" in config["metadata"].keys():
    sys.exit("The param metadata/strands must be defined when analysing paired end data")

if not "tophat" in config.keys():
    config["tophat"] = {}

if not "threads" in config["tophat"].keys():
    config["tophat"]["threads"] = "1"

if not "max_mismatches" in config["tophat"].keys():
    config["tophat"]["max_mismatches"] = "2"

if not "fastq" in config["dir"].keys():
    sys.exit("You must define a location for the fastq file.")

fastq_dir = os.path.join(config["dir"]["base"], config["dir"]["fastq"])
sample_dir = os.path.join(config["dir"]["base"], config["dir"]["samples"])
seq_type = config["metadata"]["seq_type"]

# Define input files
def tophat_inputs(wildcards):
    seq_type = config["metadata"]["seq_type"]
    if (seq_type == "pe"):
        reads = expand("{fastq_dir}/{samples}/{prefix}_{strand}.fastq", strand=config["metadata"]["strands"].split(), samples=wildcards.samples, fastq_dir=fastq_dir, prefix=wildcards.prefix)
    elif (seq_type == "se"):
        reads = expand("{fastq_dir}/{samples}/{prefix}.fastq", samples=wildcards.samples, fastq_dir=fastq_dir, prefix=wildcards.prefix)
    else:
        sys.exit("Error: invalid sequencing type parameter. Must be 'se' or 'pe'")
    return reads

# Rule
rule tophat:
    """Read mapping for single or paired end data using Tophat. 
    Requires the indexing to have previously been done (using the
    rule bowtie2_index).

    Required parameters:
        config["dir"]["genome"]
        config["genome"]["version"]
        config["genome"]["fasta_file"]
        config["qsub"]


    Optional parameters:
        config["tophat"]["max_mismatches"]
        config["tophat"]["threads"]

    Copyright Claire Rioualen
    """
    input:
        reads = tophat_inputs,
        index = os.path.join(config["dir"]["genome"], "bowtie2", config["genome"]["fasta_file"])
    output:
        bam = sample_dir + "/{samples}/{prefix}_tophat.bam"
    params:
        outdir = sample_dir + "/{samples}/{prefix}_tophat", \
        max_mismatches = config["tophat"]["max_mismatches"], \
        threads = config["tophat"]["threads"], \
        qsub = config["qsub"] \
          + " -e " + sample_dir + "/{samples}/{prefix}_tophat_qsub.err" \
          + " -o " + sample_dir + "/{samples}/{prefix}_tophat_qsub.out"
    log: sample_dir + "/{samples}/{prefix}_tophat.log"
    benchmark: sample_dir + "/{samples}/{prefix}_tophat_benchmark.json"
    run:
        if (seq_type == "pe"):
            shell("tophat --num-threads {params.threads} --read-mismatches {params.max_mismatches} --output-dir {params.outdir} {input.index} " + input.reads[0] + " " + input.reads[1] + " 2> {log}")
        elif (seq_type == "se"):
            shell("tophat --num-threads {params.threads} --read-mismatches {params.max_mismatches} --output-dir {params.outdir} {input.index} {input.reads} 2> {log}")
        shell("mv {params.outdir}/accepted_hits.bam {output.bam} 2> {log}")

