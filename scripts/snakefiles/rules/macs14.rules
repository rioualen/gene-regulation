# Set parameters
if not "qsub" in config.keys():
    sys.exit("The parameter qsub must be defined in the config file")

if not "size" in config["genome"].keys():
    sys.exit("The parameter genome size must be defined in the config file")

if not "macs14" in config.keys():
    config["macs14"] = {}

if not "pval" in config["macs14"].keys():
    config["macs14"]["pval"] = "0.00001"

if not "mfold" in config["macs14"].keys():
    config["macs14"]["mfold"] = "10,30"

if not "bandwidth" in config["macs14"].keys():
    config["macs14"]["bandwidth"] = "300"

if not "keep_dup" in config["macs14"].keys():
    config["macs14"]["keep_dup"] = "auto"

if not "other_options" in config["macs14"].keys():
    config["macs14"]["other_options"] = ""

if not "samples" in config["dir"].keys():
    sys.exit("The sample dir must be defined in the config file")

if not "peaks" in config["dir"].keys():
    sys.exit("The peaks dir must be defined in the config file")

dir_samples = config["dir"]["samples"]
dir_macs14 = os.path.join(config["dir"]["peaks"], "{treatment}_vs_{control}/macs14")

# Rule
rule macs14:
    """Peak-calling with macs14.
    Input: bed (others supported)
    Output: bed

    Required parameters:
        config["genome"]["size"]
        config["qsub"]
        config["dir"]["samples"]
        config["dir"]["peaks"]

    Optional parameters:
        config["macs14"]["pval"]
        config["macs14"]["keep_dup"]
        config["macs14"]["bandwidth"]
        config["macs14"]["mfold"]
        config["macs14"]["other_options"]

    Copyright Claire Rioualen, Lucie Khamvongsa
    """
    input:
        treatment = dir_samples + "/{treatment}/{treatment}{preprocess,.*}_sorted_pos.bed",
        control=dir_samples + "/{control}/{control}{preprocess,.*}_sorted_pos.bed"
    output:
        peaks_bed = dir_macs14 + "/{treatment}_vs_{control}{preprocess,.*}_macs14.bed"
    params:
        outdir = dir_macs14,
        name = "{treatment}_vs_{control}{preprocess,.*}_macs14",
        pval = config["macs14"]["pval"],
        keep_dup = config["macs14"]["keep_dup"],
        mfold = config["macs14"]["mfold"],
        bandwidth = config["macs14"]["bandwidth"],
        other_options = config["macs14"]["other_options"],
        genome_size = config["genome"]["size"],
        qsub=config["qsub"] \
          + " -e " + dir_macs14 + "/{treatment}_vs_{control}{preprocess,.*}_macs14_qsub.err" \
          + " -o " + dir_macs14 + "/{treatment}_vs_{control}{preprocess,.*}_macs14_qsub.out"
    log: dir_macs14 + "/{treatment}_vs_{control}{preprocess,.*}_macs14.log"
    benchmark: dir_macs14 + "/{treatment}_vs_{control}{preprocess,.*}_macs14_benchmark.txt"
    shell: """
(mkdir -p {params.outdir}
macs -t {input.treatment} -c {input.control} -f BED -g {params.genome_size} -m {params.mfold} --pvalue {params.pval} --keep-dup {params.keep_dup} --outdir {params.outdir} -n {params.name} --bw {params.bandwidth} {params.other_options}
mv {params.outdir}/{params.name}_peaks.bed {params.outdir}/{params.name}.bed ;
) &> {log}
"""

